apply plugin: 'com.android.application'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 6
def versionBuild = 0

android {
    signingConfigs {
        config {
            keyAlias 'b2c'
            keyPassword 'bYQrb9re'
            storeFile file('./b2c.jks')
            storePassword 'bYQrb9re'
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.cobis.tuiiob2c"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode = getVersionCode(versionMajor, versionMinor, versionPatch, versionBuild)
        versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/MANIFEST'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/MANIFEST.MF'
    }
    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField("boolean", "STORE", "false")
            signingConfig signingConfigs.config
        }
        quality{
            minifyEnabled true
            shrinkResources = true
            debuggable false
            proguardFiles 'proguard-butterknife.pro'
            proguardFiles 'proguard-google-analytics.pro'
            proguardFiles 'proguard-google-play-services.pro'
            proguardFiles 'proguard-gson.pro'
            proguardFiles 'proguard-retrofit2.pro'
            proguardFiles 'proguard-rules.pro'
            proguardFiles 'proguard-rx-java.pro'
            proguardFiles 'proguard-sqlite.pro'
            proguardFiles 'proguard-square-okhttp3.pro'
            proguardFiles 'proguard-support-design.pro'
            proguardFiles 'proguard-support-v7-appcompat.pro'
            proguardFiles 'proguard-support-v7-cardview.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                renameAPK(variant, defaultConfig)
            }
            buildConfigField("boolean", "STORE", "false")
            signingConfig signingConfigs.config
        }
        release {
            initWith quality
            buildConfigField("boolean", "STORE", "true")
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    flavorDimensions "default"
    productFlavors{
        dev{
            dimension "default"
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "Tuiio Móvil (dev)"
            buildConfigField("String", "ENVIRONMENT_DEV", "\"${environment_dev}\"")
            buildConfigField("String", "ENVIRONMENT_SAFE", "\"${environment_safe}\"")
            buildConfigField("String", "ENVIRONMENT_DEV_COBIS", "\"${environment_dev_cobis}\"")
            buildConfigField("String", "ENVIRONMENT_SUSTAINING", "\"${environment_sustaining}\"")
            buildConfigField("String", "ENVIRONMENT_SUSTAINING_2", "\"${environment_sustaining_2}\"")
            buildConfigField("String", "ENVIRONMENT_TEST", "\"${environment_test}\"")
            buildConfigField("String", "ENVIRONMENT_TEST_COBIS", "\"${environment_test_cobis}\"")
            buildConfigField("String", "ENVIRONMENT_PROD", "\"${environment_dev}\"")
            buildConfigField("String", "ENVIRONMENT_DRP", "\"${environment_dev_drp}\"")
            externalNativeBuild.cmake {
                cppFlags '-DDEV'
            }
        }
        sust{
            dimension "default"
            applicationIdSuffix ".sust"
            resValue "string", "app_name", "Tuiio Móvil"
            buildConfigField("String", "ENVIRONMENT_SUSTAINING", "\"${environment_sustaining}\"")
            buildConfigField("String", "ENVIRONMENT_SUSTAINING_2", "\"${environment_sustaining_2}\"")
            buildConfigField("String", "ENVIRONMENT_PROD", "\"${environment_prod}\"")
            buildConfigField("String", "ENVIRONMENT_DRP", "\"${environment_prod_drp}\"")
            externalNativeBuild.cmake {
                cppFlags '-DSUST'
            }
        }
        prod{
            dimension "default"
            resValue "string", "app_name", "Tuiio Móvil"
            buildConfigField("String", "ENVIRONMENT_PROD", "\"${environment_prod}\"")
            buildConfigField("String", "ENVIRONMENT_DRP", "\"${environment_prod_drp}\"")
            externalNativeBuild.cmake {
                cppFlags '-DPROD'
            }
        }
    }
}

static def renameAPK(variant, defaultConfig) {
    variant.outputs.all { output ->
        outputFileName = "Tuiio_v" + defaultConfig.versionName + "(" + defaultConfig.versionCode + ").apk"
    }
}

static def getVersionCode(versionMajor, versionMinor, versionPatch, versionBuild) {
    return versionMajor * 100000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.bayteq:libCore:2.0.0_rc19'
    implementation 'com.google.dagger:dagger:2.16'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.jakewharton:butterknife:9.0.0-SNAPSHOT'
    implementation 'com.scottyab:secure-preferences-lib:0.1.4'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.4'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.16'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0-SNAPSHOT'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
